plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.plugin.compose"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.facebook.react"
    id "com.google.gms.google-services"
}

react {
    autolinkLibrariesWithApp()
}

def enableProguardInReleaseBuilds = false

def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.sipcallapp"
    defaultConfig {
        applicationId "com.sipcallapp"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        def properties = new Properties()
        def localPropertiesFile = file("${rootDir}/local.properties")

        if (localPropertiesFile.exists()) {
            properties.load(new FileInputStream(localPropertiesFile))
        }

        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", "false" // Tắt tạm thời
        buildConfigField "boolean", "IS_HERMES_ENABLED", "true" // Tùy chỉnh theo nhu cầu
        buildConfigField "boolean", "IS_FABRIC_ENABLED", "false"

        buildConfigField "String", "TEST_SIP_USERNAME", "\"${properties.getProperty('TEST_SIP_USERNAME', 'default_username')}\""
        buildConfigField "String", "TEST_SIP_PASSWORD", "\"${properties.getProperty('TEST_SIP_PASSWORD', 'default_password')}\""
        buildConfigField "String", "TEST_SIP_CALLER_NAME", "\"${properties.getProperty('TEST_SIP_CALLER_NAME', 'default_callername')}\""
        buildConfigField "String", "TEST_SIP_CALLER_NUMBER", "\"${properties.getProperty('TEST_SIP_CALLER_NUMBER', 'default_callernumber')}\""
        buildConfigField "String", "TEST_SIP_DEST_NUMBER", "\"${properties.getProperty('TEST_SIP_DEST_NUMBER', 'default_dest_number')}\""
        buildConfigField "String", "PRECALL_DIAGNOSIS_NUMBER", "\"${properties.getProperty('PRECALL_DIAGNOSIS_NUMBER', 'default_precall_number')}\""

    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    buildFeatures {
        compose true
        buildConfig true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = rootProject.ext.composeCompilerVersion
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation("com.facebook.react:react-android")
    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }

    // AndroidX & Compose
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.0"
    implementation "androidx.activity:activity-compose:1.9.0"
    implementation platform("androidx.compose:compose-bom:2024.05.00")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-graphics"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.0"
    implementation "androidx.navigation:navigation-compose:2.8.0"
    implementation "androidx.compose.runtime:runtime-livedata:1.6.7"

    // Telnyx SDK module
    implementation project(":telnyx_common")

    // Firebase & other libs
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "com.google.firebase:firebase-messaging-ktx:24.1.0"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.karumi:dexter:6.2.3"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.1"

    // Tests
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}

apply from: "../../node_modules/react-native-vector-icons/fonts.gradle"